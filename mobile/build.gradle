buildscript {
    dependencies {
        classpath libraries.build.androidGradle
        classpath libraries.build.buildProperties
        classpath libraries.build.fabric
        classpath libraries.build.kotlinGradle
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.novoda.build-properties'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

buildProperties {
    letterboxd {
        file project.file('../letterboxd.properties')
    }
    fabric {
        file project.file('../fabric.properties')
    }
}

android {
    compileSdkVersion versions.androidSdk.compile
    buildToolsVersion versions.androidSdk.buildTools

    defaultConfig {
        applicationId "com.ataulm.whatsnext"
        minSdkVersion versions.androidSdk.min
        targetSdkVersion versions.androidSdk.target
        versionCode 1
        versionName "1.0"

        manifestPlaceholders = [
                fabricApiKey: buildProperties.fabric['apiKey'].string
        ]

        buildConfigString 'LETTERBOXD_KEY', buildProperties.letterboxd['apiKey'].string
        buildConfigString 'LETTERBOXD_SECRET', buildProperties.letterboxd['apiSecret'].string
        buildConfigString 'LETTERBOXD_USERNAME', buildProperties.letterboxd['username'].string
        buildConfigString 'LETTERBOXD_PASSWORD', buildProperties.letterboxd['password'].string

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation(libraries.crashlytics) { transitive = true }
    implementation libraries.glide
    implementation libraries.gson
    implementation libraries.kotlinStdLib
    implementation libraries.okhttp
    implementation libraries.rxandroid
    implementation libraries.rxjava
    implementation libraries.supportAppCompatV7
    implementation libraries.supportDesign
    implementation libraries.supportRecyclerView
    implementation libraries.supportV13

    implementation libraries.butterknife
    kapt libraries.butterknifeCompiler

    // Android runner and rules support
    androidTestImplementation(libraries.androidTestRunner) {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    androidTestImplementation(libraries.androidTestRules) {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    // Espresso support
    androidTestImplementation(libraries.espressoCore) {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    testImplementation libraries.jUnit
    testImplementation libraries.mockitoCore
    testImplementation libraries.festAssertCore
}
